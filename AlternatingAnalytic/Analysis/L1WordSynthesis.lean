import AlternatingAnalytic.Analysis.L1WordBlocks
import Mathlib.Analysis.Normed.Module.Multilinear.Curry
import Mathlib.Analysis.Normed.Module.Completion
import Mathlib.Analysis.Analytic.Basic

/-!
# Synthesis from bounded word coefficients

A bounded family of vectors in a Banach space defines a bounded linear map on ℓ¹. Applied to
the scaled coefficients `s^n p_n(e_{a_1}, …, e_{a_n})` of a power series, this gives the map
`T` in the proof of Theorem 4.5(2), with values in the completion of `H`, and `T` recovers
each homogeneous term on its word block. The scalar field need not be complete.
-/

open scoped BigOperators lp
open Filter Topology

noncomputable section

namespace L1Coordinates

variable {K J W : Type*} [NontriviallyNormedField K]
  [NormedAddCommGroup W] [NormedSpace K W]

local instance : DecidableEq J := Classical.decEq J

/-- Continuous multilinear maps on ℓ¹ are determined by their values on basis vectors. -/
theorem ext_single {d : ℕ}
    {C D : ContinuousMultilinearMap K (fun _ : Fin d => lp (fun _ : J => K) 1) W}
    (h : ∀ a : Fin d → J,
      C (fun r => lp.single 1 (a r) (1 : K)) =
        D (fun r => lp.single 1 (a r) (1 : K))) : C = D := by
  classical
  ext x
  have hs (s : Finset J) :
      C (fun r => ∑ j ∈ s, lp.single 1 j (x r j)) =
        D (fun r => ∑ j ∈ s, lp.single 1 j (x r j)) := by
    simp only [map_sum_single, h]
  exact tendsto_nhds_unique (tendsto_map_sum_single C x)
    (by simpa only [hs] using tendsto_map_sum_single D x)

variable [CompleteSpace W]

/-- The bounded linear map on ℓ¹ sending each basis vector `e_j` to `c j`. -/
def linearOfBounded (c : J → W) (M : ℝ) (hc : ∀ j, ‖c j‖ ≤ M) :
    lp (fun _ : J => K) 1 →L[K] W :=
  continuousMultilinearCurryFin1 K (lp (fun _ : J => K) 1) W
    (continuousMultilinearOfBounded (fun a : Fin 1 → J => c (a 0)) M (fun a => hc (a 0)))

@[simp]
theorem linearOfBounded_single (c : J → W) (M : ℝ) (hc : ∀ j, ‖c j‖ ≤ M) (j : J) :
    linearOfBounded (K := K) c M hc (lp.single 1 j (1 : K)) = c j := by
  classical
  have hs : Fin.snoc 0 (lp.single (E := fun _ : J => K) 1 j (1 : K)) =
      fun _ : Fin 1 => lp.single 1 j (1 : K) := by
    ext i
    fin_cases i
    rfl
  simp only [linearOfBounded, continuousMultilinearCurryFin1_apply, hs]
  exact continuousMultilinearOfBounded_single _ _ _ (fun _ => j)

theorem norm_linearOfBounded_le (c : J → W) (M : ℝ) (hM : 0 ≤ M)
    (hc : ∀ j, ‖c j‖ ≤ M) : ‖linearOfBounded (K := K) c M hc‖ ≤ M := by
  rw [linearOfBounded, LinearIsometryEquiv.norm_map]
  exact continuousMultilinearOfBounded_norm_le _ M hM _

section WordSynthesis

variable {I H : Type*} [NormedAddCommGroup H] [NormedSpace K H]

local instance : DecidableEq I := Classical.decEq I
local instance : DecidableEq (Σ n : ℕ, Fin n → I) := Classical.decEq _

/-- The coefficient `s^n p_n(e_{a_1}, …, e_{a_n})` of the word `a`, in the completion of `H`. -/
def wordCoefficient (p : FormalMultilinearSeries K (lp (fun _ : I => K) 1) H)
    (s : K) (j : Σ n : ℕ, Fin n → I) : UniformSpace.Completion H :=
  s ^ j.1 • ((p j.1 (fun i => lp.single 1 (j.2 i) (1 : K)) : H) :
    UniformSpace.Completion H)

theorem norm_wordCoefficient_le
    (p : FormalMultilinearSeries K (lp (fun _ : I => K) 1) H) (s : K) (M : ℝ)
    (hp : ∀ n, ‖p n‖ * ‖s‖ ^ n ≤ M) (j : Σ n : ℕ, Fin n → I) :
    ‖wordCoefficient p s j‖ ≤ M := by
  rw [wordCoefficient, norm_smul, norm_pow, UniformSpace.Completion.norm_coe]
  have hbound : ‖p j.1 (fun i => lp.single 1 (j.2 i) (1 : K))‖ ≤ ‖p j.1‖ := by
    simpa using (p j.1).le_opNorm (fun i => lp.single 1 (j.2 i) (1 : K))
  calc
    ‖s‖ ^ j.1 * ‖p j.1 (fun i => lp.single 1 (j.2 i) (1 : K))‖
        ≤ ‖s‖ ^ j.1 * ‖p j.1‖ := mul_le_mul_of_nonneg_left hbound (by positivity)
    _ ≤ M := by simpa only [mul_comm] using hp j.1

/-- The bounded linear map on ℓ¹(Words I, K) given by the word coefficients. -/
def wordSynthesis (p : FormalMultilinearSeries K (lp (fun _ : I => K) 1) H)
    (s : K) (M : ℝ) (hp : ∀ n, ‖p n‖ * ‖s‖ ^ n ≤ M) :
    lp (fun _ : (Σ n : ℕ, Fin n → I) => K) 1 →L[K] UniformSpace.Completion H :=
  linearOfBounded (wordCoefficient p s) M (norm_wordCoefficient_le p s M hp)

@[simp]
theorem wordSynthesis_single
    (p : FormalMultilinearSeries K (lp (fun _ : I => K) 1) H)
    (s : K) (M : ℝ) (hp : ∀ n, ‖p n‖ * ‖s‖ ^ n ≤ M) (n : ℕ) (a : Fin n → I) :
    wordSynthesis p s M hp (lp.single 1 ⟨n, a⟩ (1 : K)) =
      s ^ n • ((p n (fun i => lp.single 1 (a i) (1 : K)) : H) :
        UniformSpace.Completion H) := by
  exact linearOfBounded_single _ _ _ _

theorem norm_wordSynthesis_le
    (p : FormalMultilinearSeries K (lp (fun _ : I => K) 1) H)
    (s : K) (M : ℝ) (hM : 0 ≤ M) (hp : ∀ n, ‖p n‖ * ‖s‖ ^ n ≤ M) :
    ‖wordSynthesis p s M hp‖ ≤ M :=
  norm_linearOfBounded_le _ M hM _

/-- Synthesis recovers the homogeneous term `p n` on the degree `n` word block. -/
theorem wordSynthesis_wordBlock
    (p : FormalMultilinearSeries K (L1 K I) H) (s : K) (hs : s ≠ 0)
    (M : ℝ) (hp : ∀ n, ‖p n‖ * ‖s‖ ^ n ≤ M)
    (n : ℕ) (x : Fin n → L1 K I) :
    wordSynthesis p s M hp (wordBlock s n x) =
      (p n x : UniformSpace.Completion H) := by
  have heq : (wordSynthesis p s M hp).compContinuousMultilinearMap (wordBlock s n) =
      ContinuousLinearMap.compContinuousMultilinearMap
        (UniformSpace.Completion.toComplL : H →L[K] UniformSpace.Completion H) (p n) := by
    apply ext_single
    intro a
    change wordSynthesis p s M hp (wordBlock s n (fun i => lp.single 1 (a i) (1 : K))) = _
    rw [wordBlock_single, map_smul, wordSynthesis_single, smul_smul]
    simp [hs]
  exact congrArg (fun C => C x) heq

end WordSynthesis

end L1Coordinates
